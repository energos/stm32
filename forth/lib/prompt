\ Hey Emacs, this is a Forth file -*- mode: Forth; comment-column: 40; -*-

\ Copied with minors mods from
\ ../upstream/embello/explore/1608-forth/flib/mecrisp/prompt.fs
\ ../upstream/mecrisp-stellaris-2.5.5/common/prompt.txt

\ Prompt by Glen Worstell
\ copied with minor mods from Mecrisp Stellaris "common/prompt.txt"

#require colors

: red-emit red emit ansi-reset ;

\ printing in decimal
: .# base @ swap decimal 0 <# #s #> type base ! ;

\ If a "word" that calls "init" is loaded first, omit next line.
: init ;
\ Glens prompt
: prompt ( -- )
    begin
        ."  ("
        \ show where compiling
        compiletoram? if [char] R emit else [char] F red-emit then
        base @
        \ show base
        case
            #10 of [char] # emit endof
            #16 of [char] $ red-emit endof
            #2  of [char] % red-emit endof
            base @ magenta .# [char] b emit ansi-reset \ show base if not # $ %.
        endcase
        \ show stack depth
        depth dup if red .# ansi-reset else .# then
        ." ) ok." cr   \ show ">" for prompt. Could show "OK."
        query interpret
    again
;

: init init ['] prompt hook-quit ! ; \ make new prompt

init quit
